plugins {
    id 'java'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

group = mod_group
version = mod_version

base {
    archivesName = 'northstar-curios-compat'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    maven {
        url = 'https://cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly fg.deobf("curse.maven:northstar-redux-${northstar_redux_cf}")
    compileOnly fg.deobf("curse.maven:curios-${curios_cf}")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, 'northstar_curios_compat.refmap.json'
    config 'northstar_curios_compat.mixins.json'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

jar {
    manifest {
        attributes(
            'Specification-Title': mod_name,
            'Specification-Version': mod_version,
            'Implementation-Title': project.name,
            'Implementation-Version': mod_version,
            'Implementation-Vendor': mod_authors,
            'MixinConfigs': 'northstar_curios_compat.mixins.json'
        )
    }
}
